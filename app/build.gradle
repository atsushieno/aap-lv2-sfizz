plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "org.androidaudioplugin.ports.lv2.sfizz"
        minSdkVersion 29
        targetSdkVersion 33
        versionCode 1
        versionName "$aap_lv2_version"

        externalNativeBuild {
            cmake {
                arguments "-DSFIZZ_USE_SNDFILE=0", "-DENABLE_LTO=off", "-DSFIZZ_VST=off", "-DSFIZZ_LV2_UI=off", "-DSFIZZ_JACK=off", "-DSFIZZ_RENDER=off", "-DSFIZZ_SHARED=off", "-DANDROID_STL=c++_shared", "-DAAP_ENABLE_ASAN=" + (enable_asan ? "1" : "0")
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    ndkVersion "$ndk_version"

    buildTypes {
        debug {
            packagingOptions {
                doNotStrip "**/*.so"
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "../external/sfizz/CMakeLists.txt"
        }
    }
    buildFeatures {
        prefab true
    }

    aaptOptions {
        noCompress 'wav'
    }
    packagingOptions {
        jniLibs.useLegacyPackaging = enable_asan
    }
}

dependencies {
    implementation libs.aap.lv2
    implementation libs.aap.core
    implementation libs.aap.midi.device.service
    implementation libs.aap.ui.compose
    implementation libs.androidx.appcompat
    implementation libs.startup.runtime

    testImplementation libs.junit
    androidTestImplementation libs.aap.testing
    androidTestImplementation libs.test.rules
    androidTestImplementation libs.test.ext.junit
}


task deleteLV2Manifests(type: Delete) {
    delete 'src/main/assets/lv2/sfizz.lv2'
}

tasks.whenTaskAdded { task ->
    // FIXME: it's hacky, but `clean` didn't work.
    if (task.name == "externalNativeBuildCleanDebug" || task.name == "externalNativeBuildCleanRelease")
        task.dependsOn deleteLV2Manifests
}
