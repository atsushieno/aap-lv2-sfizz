plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 30
        targetSdkVersion 32
        versionCode 1
        versionName "$aap_lv2_version"

        externalNativeBuild {
            cmake {
                arguments "-DSFIZZ_USE_SNDFILE=0", "-DENABLE_LTO=off", "-DSFIZZ_VST=off", "-DSFIZZ_LV2_UI=off", "-DSFIZZ_JACK=off", "-DSFIZZ_RENDER=off", "-DSFIZZ_SHARED=off", "-DANDROID_STL=c++_shared", "-DAAP_ENABLE_ASAN=" + (enable_asan ? "1" : "0")
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    ndkVersion "$ndk_version"

    buildTypes {
        debug {
            packagingOptions {
                doNotStrip "**/*.so"
                pickFirst "**/sfizz.so"
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "../external/sfizz/CMakeLists.txt"
        }
    }
    // FIXME: enable these sections once we migrated to prefab.
    /*
    buildFeatures {
        prefab true
    }
    */

    aaptOptions {
        noCompress 'wav'
    }
    packagingOptions {
        jniLibs.useLegacyPackaging = enable_asan
    }
}

dependencies {
    implementation "org.androidaudioplugin:androidaudioplugin:$aap_version"
    implementation "org.androidaudioplugin:androidaudioplugin-ui-compose:$aap_version"
    implementation "org.androidaudioplugin:androidaudioplugin-midi-device-service:$aap_version"
    implementation "org.androidaudioplugin:androidaudioplugin-lv2:$aap_lv2_version"
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.startup:startup-runtime:1.1.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation "org.androidaudioplugin:androidaudioplugin-testing:$aap_version"
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
}


task deleteLV2Manifests(type: Delete) {
    delete 'src/main/assets/lv2/sfizz.lv2'
}

tasks.whenTaskAdded { task ->
    // FIXME: it's hacky, but `clean` didn't work.
    if (task.name == "externalNativeBuildCleanDebug" || task.name == "externalNativeBuildCleanRelease")
        task.dependsOn deleteLV2Manifests
}
